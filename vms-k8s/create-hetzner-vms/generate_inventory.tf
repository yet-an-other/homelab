locals {
  # Format for Ansible inventory YAML
  inventory_yaml = yamlencode({
    all = {
      vars = {
        "cluster_env" = "hetzner"
        "root_domain_name" = var.root_domain_name
        "main_domain_name" = var.main_domain_name
      }
      children = {
        "k8s-nodes" = {
          hosts = merge(
            # Control nodes
            {
              for key, value in local.control_servers_map : key => {
                ansible_host                 = value.ipv4
                ansible_user                 = "root"
                ansible_port                 = 9022
                ansible_ssh_private_key_file = var.ssh_key_path
                public_ip                    = hcloud_load_balancer.control.ipv4
                internal_ip                  = value.internal_ip
              }
            },
            # Worker nodes
            {
              for key, value in local.worker_servers_map : key => {
                ansible_host                 = value.ipv4
                ansible_user                 = "root"
                ansible_port                 = 9022
                ansible_ssh_private_key_file = var.ssh_key_path
                public_ip                    = hcloud_load_balancer.worker.ipv4
                internal_ip                  = value.internal_ip
              }
            }
          )
        },
        "control-nodes" = {
          hosts = {
            for key, _ in local.control_servers_map : key => {}
          }
        },
        "worker-nodes" = {
          hosts = {
            for key, _ in local.worker_servers_map : key => {}
          }
        }
      }
    }
  })

  # Re-map servers for easier access in the template
  control_servers_map = {
    for idx, server in hcloud_server.control :
    "cloud-control-${idx + 1}" => {
      ipv4        = server.ipv4_address
      internal_ip = hcloud_server_network.control_network[idx].ip
    }
  }

  worker_servers_map = {
    for idx, server in hcloud_server.worker :
    "cloud-worker-${idx + 1}" => {
      ipv4        = server.ipv4_address
      internal_ip = hcloud_server_network.worker_network[idx].ip
    }
  }
}

# Generate inventory file
resource "local_file" "inventory" {
  content  = "# Generated by Terraform\n${local.inventory_yaml}"
  filename = "${path.module}/../envs/hetzner/generated.inventory.yaml"

  # Make sure the directory exists
  provisioner "local-exec" {
    command = "mkdir -p ${path.module}/../envs/hetzner"
  }
}
